apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply from: 'config/config.gradle'

def version = getBuildTypeProperties("default")['version']

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "network.omisego.omgwallet"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            def debugConfig = getBuildTypeProperties("default")
            buildConfigField 'String', "CONFIG_KEY_ALIAS", debugConfig['key_alias']
            buildConfigField 'String', "CONFIG_IV", debugConfig['iv']
            buildConfigField 'String', "CONFIG_REDIRECT_URL", debugConfig['redirect_url']
        }

        // For instrumental tests
        staging {
            initWith(buildTypes.debug)
            def stagingConfig = getTestBuildTypeProperties("staging")
            buildConfigField 'String', "CONFIG_TEST_MASTER_ACC_WALLET_ADDRESS", stagingConfig['test_master_acc_wallet_address']
            buildConfigField 'String', "CONFIG_TEST_ADMIN_EMAIL", stagingConfig['test_admin_email']
            buildConfigField 'String', "CONFIG_TEST_ADMIN_PASSWORD", stagingConfig['test_admin_password']
            buildConfigField 'String', "CONFIG_TEST_USER_EMAIL", stagingConfig['test_user_email']
            buildConfigField 'String', "CONFIG_TEST_USER_PASSWORD", stagingConfig['test_user_password']
            buildConfigField 'String', "CONFIG_TEST_CONSUMER_EMAIL", stagingConfig['test_tx_consumer_email']
            buildConfigField 'String', "CONFIG_TEST_CONSUMER_PASSWORD", stagingConfig['test_tx_consumer_password']
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            def releaseConfig = getBuildTypeProperties("release")
            buildConfigField 'String', "CONFIG_KEY_ALIAS", releaseConfig['key_alias']
            buildConfigField 'String', "CONFIG_IV", releaseConfig['iv']
            buildConfigField 'String', "CONFIG_REDIRECT_URL", releaseConfig['redirect_url']
        }
    }
    testBuildType "staging"
    flavorDimensions "environment"
    productFlavors {
        local {
            dimension "environment"
            applicationIdSuffix ".local"
            versionNameSuffix "-local"
            def localConfig = getFlavorProperties("local")
            buildConfigField 'String', "CONFIG_API_KEY", localConfig['api_key']
            buildConfigField 'String', "CONFIG_BASE_URL", localConfig['base_url']
            buildConfigField 'String', "CONFIG_SOCKET_BASE_URL", localConfig['socket_base_url']

        }
        demo {
            dimension "environment"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
            def demoConfig = getFlavorProperties("demo")
            buildConfigField 'String', "CONFIG_API_KEY", demoConfig['api_key']
            buildConfigField 'String', "CONFIG_BASE_URL", demoConfig['base_url']
            buildConfigField 'String', "CONFIG_SOCKET_BASE_URL", demoConfig['socket_base_url']
        }
        coffeego {
            dimension "environment"
            applicationIdSuffix ".coffeego"
            versionNameSuffix "-coffeego"
            def coffeeGoConfig = getFlavorProperties("coffeego")
            buildConfigField 'String', "CONFIG_API_KEY", coffeeGoConfig['api_key']
            buildConfigField 'String', "CONFIG_BASE_URL", coffeeGoConfig['base_url']
            buildConfigField 'String', "CONFIG_SOCKET_BASE_URL", coffeeGoConfig['socket_base_url']
        }
    }
    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/java'
        androidTest.resources.srcDirs += 'src/androidTest/resources'
    }
    buildToolsVersion '28.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        disable 'ParcelCreator'
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        if (variant.name.toLowerCase().contains("debug")) {
            outputFileName = "pos-client-debug-${variant.versionName}.apk"
        } else {
            outputFileName = "pos-client-release-${variant.versionName}.apk"
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    /* OmiseGO */
    implementation deps.omisego
    /* Kotlin */
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutine
    implementation deps.kotlin.coroutineAndroid
    /* Navigation Component */
    implementation deps.navigation.fragment
    implementation deps.navigation.ui
    /* Architecture Component */
    implementation deps.arch.viewmodel
    implementation deps.arch.extensions
    /* Databinding */
    /* Support library */
    implementation deps.support.design
    implementation deps.support.appCompat
    implementation deps.support.cardView
    /* Networking */
    implementation deps.retrofit.core
    implementation deps.retrofit.converterGson
    implementation deps.retrofit.adapterRxJava2
    /* Reactive */
    implementation deps.rx2
    /* Load image */
    implementation deps.glide
    implementation deps.constraintLayout
    implementation deps.shimmer
    implementation deps.stetho
    implementation deps.dexter
    implementation deps.biometric
    implementation deps.pagerindicator
    implementation deps.okHttp3.loggingIntercepter

    /* Debugging */
    implementation deps.firebase.core
    implementation deps.firebase.crashlytics

    /* Testing */
    implementation deps.idlingResource

    testImplementation deps.test
    androidTestImplementation deps.androidTest
    androidTestImplementation (deps.omisegoAdmin){
        exclude group: 'co.omisego', module:'omisego-core'
    }
    androidTestImplementation deps.okHttp3.mockWebserver
}

apply plugin: 'com.google.gms.google-services'
